model {
   for(i in 1:n) { # The likelihood is specified here
      FEV[i] ~ dnorm(mu[i],tau)
      mu[i] <- beta[1] + beta[2]*Age[i] + beta[3]*Smoke[i] +
               beta[4]*Age[i]*Smoke[i]
    }
# Priors on beta and tau are specified here
    beta[1:p] ~ dmnorm(beta0[1:p],C0inv[1:p,1:p])
    tau ~ dgamma(a,b)
    sigma <- 1/sqrt(tau)
# Estimate mean FEV for S and NS aged 10-19
   for(i in 1:10) {
      meanFEVs[i]  <-  beta[1]+(beta[2]+beta[4])*(i+9)+beta[3]
      meanFEVns[i] <-  beta[1]+beta[2]*(i+9)
   }
# Easy to estimate relative means and mean differences as well
   RM <- meanFEVns[9]/meanFEVs[9] # RM compares 18-year-old NS to S (ratio)
   MD <- meanFEVs[9]-meanFEVns[4] # MD compares 18-year-old S to 13-year-old NS (difference)
# Predict FEV for 20 yo smoker and nonsmoker
   FEV20s ~ dnorm(mu20s,tau)
   FEV20ns ~ dnorm(mu20ns,tau)
   mu20s <- beta[1] + (beta[2]+beta[4])*20 + beta[3]
   mu20ns <- beta[1] + beta[2]*20
}
