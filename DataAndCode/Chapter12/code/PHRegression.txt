#  Example of censored data in Appendix C (C.2.1) (Leukemia Data Revisited)
#  These statements are for JAGS
#
data {
#  Set up the a[j]s
#  d[1]=0; {d[j]: j = 2,...,k} are distinct death times if
#  last observation is a death; else, d[k] is last censored time
#  Dimension of vector d is k
   a[1] <- 0
   for (i in 2:k) {
      a[i] <- (d[i-1] + d[i])/2
   }
   a[k+1] <- d[k] + (d[k] - d[k-1])/2
# d[j] is in (a[j], a[j+1]), j = 2,...,k
# Construct H[i,j] and N[i,j]
   for (i in 1:n) {
      for (j in 1:k) {
         HH[i,j] <- ifelse(y[i]-a[j] >= 0, 1, 0)
         H[i,j] <- HH[i,j]*(min(y[i],a[j+1])-a[j])
         N[i,j] <- HH[i,j]*ifelse(a[j+1]-y[i] >= 0, 1, 0)*delta[i]
# Equals 1 iff y[i] is in interval j and delta[i]=1
      }
   }
}
model {	
# Likelihood Construction
   for (i in 1:n) {
      mu[i] <- beta *(AGgroup[i])
      for (j in 1:k) {
         N[i,j] ~ dpois(theta[i,j])
         theta[i,j] <- exp(mu[i])*H[i,j]*lam[j]
      }
    }
# Prior for lambdas
    lamm <- 10000*(0.01) # exp(-a[2]*0.01) = 0.995
    lam[1] ~ dgamma(lamm,10000) # lam1 is "fixed" at 0.01
    for (j in 2:k) {  # prior on baseline hazard bits
       f[j] <- w*(a[j+1]-a[j])
       e[j] <- lamstar*f[j]
       lam[j] ~ dgamma(e[j],f[j])
    }
    lamstar <- 0.05   # Select value or set prior for lamstar
# lamstar ~ dgamma(a,ww)   a=0.05*ww    ww=0.01

#  Inferences for survival functions
   for (j in 1:k) {
      dH0[j] <- (a[j+1]-a[j])*lam[j]   # (H_0(a[j+1])-H_0(a[j]))
# Survivor function at a[j+1] = exp{-H_0(a[j+1])exp(mu)}
# Surv Fn for AG+ and AG- groups
      S.pos[j] <- pow(exp(-sum(dH0[1:j])), exp(beta[1]))
      S.neg[j] <- pow(exp(-sum(dH0[1:j])), 1)
      S.diff[j] <- S.pos[j] - S.neg[j]
   }
#  Inferences for the HRs and prior on the betas
   for (j in 1:1) {
      HR[j] <- exp(beta[j]) # relative hazard
      beta[j] ~ dnorm(0,0.000001)
   }
}
