model {
   ### Create standardized covariates & means
   for (i in 1:(oldn + predn)) {
      sAge[i] <- (age[i]-mean(age[1:oldn]))/sd(age[1:oldn])
      sYr[i] <- (yr[i]-mean(yr[1:oldn]))/sd(yr[1:oldn])
      mu[i] <- beta[1] + beta[2]*equals(stage[i],2)
               + beta[3]*equals(stage[i],3)
               + beta[4]*equals(stage[i],4)
               + beta[5]*sAge[i] + beta[6]*sYr[i]
      # 5 month survival probabilities using covariates in the data
      S[i] <- 1- phi((log(5)-mu[i])*sqrt(tau))
    # Medians corresponding to the covariates in the data
      med[i] <- exp(mu[i])
    }
   ### Model observed subjects
   for (i in 1:oldn) {
      is.censored[i] ~ dinterval(t[i], c[i])
      t[i] ~ dlnorm(mu[i], tau)
    }
    # Make predictions for combinations of covariates
    for(i in 1:predn) {
        predt[i] ~ dlnorm(mu[i + oldn], tau)
    }

   for (i in 1:6) {
      beta[i] ~ dnorm(0,0.000001)
      rm[i] <- exp(beta[i])  # RMs for each variable
   }
   tau ~ dgamma(0.001,0.001)
   sigma <- sqrt(1/tau)
}

