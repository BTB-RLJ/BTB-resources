# This is the cow abortion data from chapter 14 of the book.
model {
   meangr <- 3.485024
   meando <- 115.842
   stdgr <-  1.517102
   stddo <- 59.72637
   for (k in 1:N) {
      sGR[k] <- (GR[k] - meangr)/stdgr
      sDO[k] <- (DO[k] - meando)/stddo
   }
   for (k in 1:9) { 
      gamma[k] ~ dnorm(beta[1], taug) 
   }
   taug <- 1/pow(sigmag,2)
   sigmag ~ dunif(0,2)
   for (k in 1:N) {  
      y[k] ~ dbern(theta[k])
      logit(theta[k]) <-  beta[2]*sDO[k] + beta[3]*sGR[k]
                          + beta[4]*sDO[k]*sGR[k] + gamma[herd[k]]
   }
   beta[1] ~ dnorm (-2,1)
   for (i in 2:4) { 
       beta[i] ~ dnorm(0,1) 
   }
   for (i in 1:4) { 
      prob[i] <- ifelse(beta[i] > 0, 1, 0)   
   }
}

